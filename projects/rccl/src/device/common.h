/*************************************************************************
 * Copyright (c) 2017-2022, NVIDIA CORPORATION. All rights reserved.
 * Modifications Copyright (c) 2019-2023 Advanced Micro Devices, Inc. All rights reserved.
 *
 * See LICENSE.txt for license information
 ************************************************************************/

#ifndef NCCL_DEVICE_COMMON_H_
#define NCCL_DEVICE_COMMON_H_

#include "collectives.h"
#include "device.h"
#include "op128.h"
#include "reduce_kernel.h"
#include "device_table.h"
#include "network/unpack/unpack_defs.h"
#define NCCL_MAX_DEV_ARITY (NCCL_MAX_TREE_ARITY - 1)  // Using balanced tree instead of split tree

#define __syncwarp()

#if RCCL_HAVE_GLOBAL_DWORDX4_BUILTINS
#define STORE(DST, SRC) \
  { \
    __scoped_atomic_store_n((__attribute__((address_space(1))) __typeof__(*(DST))*)(DST), (SRC), __ATOMIC_RELAXED, \
                            __MEMORY_SCOPE_SYSTEM); \
  }
#elif defined(__GFX9__)
#define STORE(DST, SRC) \
  { \
    __atomic_store_n((DST), (SRC), __ATOMIC_RELAXED); \
  }
#else
#define STORE(DST, SRC) \
  { \
    __atomic_store_n((DST), (SRC), __ATOMIC_SEQ_CST); \
  }
#endif

#define traceKernelLaunch(launch_type, batchIx)
#define traceKernelEnd(end_type)
#define traceData(data2, data4, data8_0, data8_1)
#define traceAbort()

#if __CUDA_ARCH__ >= 700
// __grid_constant__ appears to break cuda-gdb
#define NCCL_GRID_CONSTANT __grid_constant__
#else
#define NCCL_GRID_CONSTANT
#endif

struct ncclShmemGroup {
  ncclConnInfo* recvConns[NCCL_MAX_ARITY];
  ncclConnInfo* sendConns[NCCL_MAX_ARITY];
  void* userInput;
  void* userOutput;
  void* userAcc;
  void* srcs[NCCL_MAX_ARITY + 1];
  void* dsts[NCCL_MAX_ARITY + 1];
  void* acc;
  uint64_t barrier;
  union {
    unpackGroupShmem unpack;
  } devicePlugin;
  int32_t dstSizes[NCCL_MAX_ARITY + 1];
  uint64_t redOpArgs;
};

struct ncclShmemData {
  struct ncclDevKernelArgs args;
  int channelId;
  int aborted;
  alignas(16) struct ncclKernelComm comm;
  alignas(16) struct ncclDevChannel channel;
#ifdef ENABLE_WARP_SPEED
  int warpComm;
  alignas(16) struct ncclDevChannel warpChannel[NCCL_MAX_GROUPS];
  int warpChannelId[NCCL_MAX_GROUPS];
#endif
  int batchIx, nextBatchIx;
  enum ncclDevWorkType workType;
  uint8_t directMode;
  uint16_t funcId;
  int nWorks;
  int workSize;
  uint64_t workCounter;
  bool profilerEnabled;
  struct ncclShmemGroup groups[NCCL_MAX_GROUPS];

  alignas(16) char workStorage[ncclMaxDevWorkBatchBytes()];

  alignas(16) union {
    unpackShmem unpack;
  } devicePlugin;
#ifdef ENABLE_FAULT_INJECTION
  uint64_t faults;
#endif
  uint64_t barrier_pat;
#if RCCL_TDM_STAGE_BYTES_PER_WARP
  // A separate __shared__ should work better as it does not instantiate this for LL and LL128
  alignas(RCCL_TDM_ALIGN) char tdmStage[RCCL_TDM_STAGE_BYTES_PER_WARP * (NCCL_MAX_NTHREADS / WARP_SIZE)];
#endif
};

#ifdef RCCL_DEVICE_LINKER
__shared__ ncclShmemData ncclShmem;
__shared__ ulong2 ncclShmemPerWarp[ncclShmemScratchWarpSize() * (NCCL_MAX_NTHREADS / WARP_SIZE) / sizeof(ulong2)];
#else
extern __shared__ ncclShmemData ncclShmem;
#if __CUDA_ARCH__ >= 700
extern __shared__ ulong2 ncclShmemPerWarp[/*ncclShmemDynamicSize()/sizeof(ulong2)*/];
#else
extern __shared__ ulong2
  ncclShmemPerWarp[ncclShmemScratchWarpSize() * (NCCL_MAX_NTHREADS / WARP_SIZE) / sizeof(ulong2)];
#endif
#endif

#if RCCL_TDM_STAGE_BYTES_PER_WARP
__device__ inline void* ncclTdmStageForWarp(int warp) {
  return ncclShmem.tdmStage + warp * RCCL_TDM_STAGE_BYTES_PER_WARP;
}
#endif

#ifdef ENABLE_FAULT_INJECTION
__device__ inline void insert_random_delay_per_warp() {
  if ((ncclShmem.faults & RANDOM_DELAY_ON_WARP_START) && (threadIdx.x % WARP_SIZE == 0)) {
    switch ((wall_clock64() >> (threadIdx.x / WARP_SIZE * 2)) & 0x3) {
    case 0:
      __builtin_amdgcn_s_sleep(0);
      break;
    case 1:
      __builtin_amdgcn_s_sleep(8);
      break;
    case 2:
      __builtin_amdgcn_s_sleep(16);
      break;
    case 3:
    default:
      __builtin_amdgcn_s_sleep(32);
      break;
    }
  }
}
#endif

__device__ inline void* ncclScratchForWarp(int warp) {
  return (char*)ncclShmemPerWarp + warp * ncclShmemScratchWarpSize();
}

__device__ inline void barrier_sync(int name) {
#if 0
  asm volatile("barrier.sync %0;" :: "r"(name) : "memory");
#else
  asm volatile("barrier.sync.aligned %0;" ::"r"(name) : "memory");
#endif
}
__device__ inline void barrier_sync(int name, int nThreads) {
#if 0
  asm volatile("barrier.sync %0, %1;" :: "r"(name), "r"(nThreads) : "memory");
#else
  asm volatile("barrier.sync.aligned %0, %1;" ::"r"(name), "r"(nThreads) : "memory");
#endif
}
__device__ inline void barrier_sync_aligned(int name) {
  asm volatile("barrier.sync.aligned %0;" ::"r"(name) : "memory");
}
__device__ inline void barrier_sync_aligned(int name, int nThreads) {
  asm volatile("barrier.sync.aligned %0, %1;" ::"r"(name), "r"(nThreads) : "memory");
}

__device__ inline bool barrier_red_or(bool vote, int name) {
  int ans;
  asm volatile("{ .reg .pred p;"
               "  setp.ne.s32 p, %1, 0;"
               "  barrier.red.or.pred p, %2, p; "
               "  selp.s32 %0, 1, 0, p; }"
               : "=r"(ans)
               : "r"((int)vote), "r"(name)
               : "memory");
  return bool(ans);
}
__device__ inline bool barrier_red_or(bool vote, int name, int nThreads) {
  int ans;
  asm volatile("{ .reg .pred p;"
               "  setp.ne.s32 p, %1, 0;"
               "  barrier.red.or.pred p, %2, %3, p; "
               "  selp.s32 %0, 1, 0, p; }"
               : "=r"(ans)
               : "r"((int)vote), "r"(name), "r"(nThreads)
               : "memory");
  return bool(ans);
}

// Copy 16-byte aligned data. You must call with at least `(bytes+15)/16` threads.
inline __device__ void copyToShmem16(int tid, void* dst, void const* src, int bytes) {
  int offset = 16 * tid;
  if (offset < bytes) {
    ulong2 *src2, *dst2;
    src2 = (ulong2*)((char const*)src + offset);
    dst2 = (ulong2*)((char*)dst + offset);
    dst2->x = src2->x;
    dst2->y = src2->y;
  }
}

// Must run with at least 64 threads
__device__ __forceinline__ void loadWorkBatchToShmem(int tid, int tn, struct ncclDevKernelArgs const* args,
                                                     int batchIx) {
  int lane = tid % WARP_SIZE;
  int workCursor = 0; // num works written in previous loop iterations.
  while (true) {
    struct ncclDevWorkBatch batch = ((struct ncclDevWorkBatch*)(args + 1))[batchIx];

    // fnsOfBitset[n] = index of n'th set bit in batch.offsetBitset.
    // PTX has instruction "fns" (find n-th set) but it expands to a lot of SASS,
    // since we know all lanes will be querying the same bitmask we can compute
    // much faster using shared memory.
    uint8_t* fnsOfBitset = (uint8_t*)ncclScratchForWarp(threadIdx.x / WARP_SIZE);
    int nWorks = 0;
    __syncwarp();

    if (WARP_SIZE == 64) {
      if (uint64_t(batch.offsetBitset) & (1ull << lane)) {
        int nWorksBelow = __popcll(uint64_t(batch.offsetBitset) & ((1ull << lane) - 1));
        fnsOfBitset[nWorksBelow] = lane;
      }
      nWorks = __popcll(uint64_t(batch.offsetBitset));
    } else {
      // WARP_SIZE == 32
      if (uint32_t(batch.offsetBitset) & (1u << lane)) {
        int nWorksBelow = __popc(uint32_t(batch.offsetBitset) & ((1u << lane) - 1));
        fnsOfBitset[nWorksBelow] = lane;
      }
      int nWorksLow32 = __popc(uint32_t(batch.offsetBitset)); // just of low 32 bits
      if (uint32_t(batch.offsetBitset >> 32) & (1u << lane)) {
        int nWorksBelow = nWorksLow32;
        nWorksBelow += __popc(uint32_t(batch.offsetBitset >> 32) & ((1u << lane) - 1));
        fnsOfBitset[nWorksBelow] = 32 + lane;
      }
      nWorks = nWorksLow32 + __popc(uint32_t(batch.offsetBitset >> 32)); // add high 32 bits
    }

    int workSize;
    int nPacks; // total number of packs loaded, each pack is 16 bytes
    int packInWork; // my pack index within work struct
    int dstWork; // my work index in contiguous destination shmem
    switch (batch.workType) {
    case (int)ncclDevWorkTypeP2p:
      workSize = sizeof(struct ncclDevWorkP2p);
      nPacks = nWorks * (workSize / 16);
      packInWork = tid % (workSize / 16);
      dstWork = tid / (workSize / 16);
      break;
    case (int)ncclDevWorkTypeColl:
      workSize = sizeof(struct ncclDevWorkColl);
      nPacks = nWorks * (workSize / 16);
      packInWork = tid % (workSize / 16);
      dstWork = tid / (workSize / 16);
      break;
    case (int)ncclDevWorkTypeBcast:
      workSize = sizeof(struct ncclDevWorkBcast);
      nPacks = nWorks * (workSize / 16);
      packInWork = tid % (workSize / 16);
      dstWork = tid / (workSize / 16);
      break;
    case (int)ncclDevWorkTypeCollReg:
    default:
      workSize = sizeof(struct ncclDevWorkCollReg);
      nPacks = nWorks * (workSize / 16);
      packInWork = tid % (workSize / 16);
      dstWork = tid / (workSize / 16);
      break;
    }
    if (tid == 0) {
      ncclShmem.workSize = workSize;
    }
    // We deliberately replicate these div and mod calculations into the case
    // blocks above so that they get constant divisor optimizations by the compiler.
    //   packInWork = tid%(workSize/16);
    //   dstWork = tid/(workSize/16);

    // AGV bcast fuses up to 64 works/batch (192 packs) and can exceed the loader subgroup size
    // tn, so bcast strides while coll/P2P break after one pass. tid (not pk) is preserved so
    // the nextExtends warp rotation stays correct. alignas(16) keeps bcastPacks >= 1.
    static_assert(sizeof(struct ncclDevWorkBcast) % 16 == 0 && sizeof(struct ncclDevWorkBcast) >= 16,
                  "ncclDevWorkBcast must be a non-zero multiple of the 16B pack size");
    constexpr int bcastPacks = sizeof(struct ncclDevWorkBcast) / 16; // 3 packs/work
    bool isBcast = batch.workType == (int)ncclDevWorkTypeBcast;
    for (int pk = tid; pk < nPacks; pk += tn) {
      if (isBcast) {
        dstWork = pk / bcastPacks;
        packInWork = pk - dstWork * bcastPacks;
      }
      int srcWork = fnsOfBitset[dstWork]; // find n'th set bit in batch.offsetBitset
      ulonglong2 tmp;
      // The loads done in these two cases must be kept separate since we are
      // relying on the compiler to use "ld.param" in the first one. The parameter
      // space is not generically addressable, so any attempt to load through
      // a pointer that *might* be parameter space backed will cause the
      // compiler to spill the parameter struct (4K!) to each thread's local space
      // before creating a pointer (to the spill) and decimate perf.
      //
      // An example of what not to do would be the following:
      //
      // if (condition) {
      //   // The compiler could spill parameter_variable to local space and take
      //   // the address of that, since when src is loaded below it could also
      //   // be global space.
      //   src = &parameter_variable;
      // } else {
      //   src = &global_variable;
      // }
      // memcpy(dst, src, n);
      if (ncclShmem.args.workStorageType == ncclDevWorkStorageTypeArgs) {
        char* src = (char*)args + (batch.offsetBase + srcWork * workSize + packInWork * 16);
        tmp = *(ulonglong2*)src; // becomes ld.param.v2.u64
      }
      if (ncclShmem.args.workStorageType != ncclDevWorkStorageTypeArgs) {
        char* src = (char*)ncclShmem.args.workBuf +
                    ((batch.offsetBase + srcWork * workSize + packInWork * 16) & ncclShmem.args.workMask);
        tmp = *(ulonglong2*)src; // becomes ld.v2.u64
      }
      char* dst = ncclShmem.workStorage;
      dst += (workCursor + dstWork) * workSize + packInWork * 16;
      *(ulonglong2*)dst = tmp;
      if (!isBcast) break; // coll/P2P fit in one pass; only AGV-fused bcast strides
    }
    workCursor += nWorks;

    if (batch.nextExtends) {
      batchIx += batch.nextJump;
      tid -= 2 * WARP_SIZE; // Rotate threads so we use the next two warps for next batch struct.
      if (tid < 0) tid += tn;
    } else {
      if (tid == 0) {
        ncclShmem.batchIx = batchIx;
        ncclShmem.nextBatchIx = (batch.nextJump == 0) ? -1 : (int)(batchIx + batch.nextJump);
        ncclShmem.workType = (enum ncclDevWorkType)batch.workType;
        ncclShmem.nWorks = workCursor;
        ncclShmem.funcId = batch.funcId;
      }
      break;
    }
  }
}

__device__ __forceinline__ unsigned long long int globaltimer() {
#if defined(__HIP_PLATFORM_AMD__) || defined(__HIPCC__)
  return wall_clock64();
#else
  unsigned long long int timer;
  asm volatile("mov.u64 %0, %%globaltimer;" : "=l"(timer));
  return timer;
#endif
}

template <ncclFunc_t Fn, typename T, typename RedOp, int Algo, int Proto, int USE_ACC, int COLL_UNROLL, int Pipeline,
          int UserRegMode = 0>
struct RunWorkColl {
  __device__ void run(int tid, int tn, struct ncclDevWorkColl* work) {
    // Put NOT IMPLEMENTED behavior here.
  }
};

template <ncclFunc_t Fn, typename T, typename RedOp, int Algo, int Proto, int USE_ACC, int COLL_UNROLL, int Pipeline,
          int UserRegMode = 0>
struct RunWorkBatch;

// Specialized for P2p in sendrecv.h. The add_unroll.sh hipify pass appends the trailing
// USE_ACC/COLL_UNROLL/Pipeline/UserRegMode template parameters; UserRegMode selects the
// latency-protocol kernel variant (0 = legacy LL, 1 = LL128, launched on gfx942/gfx950 only).
template <typename T, typename RedOp>
struct RunWorkBatch<ncclFuncSendRecv, T, RedOp, NCCL_ALGO_RING, NCCL_PROTO_SIMPLE>;

template <typename T, typename RedOp, int Proto>
struct RunWorkBatch<ncclFuncAllGatherV, T, RedOp, NCCL_ALGO_RING, Proto>;

// Specialized here for non-P2p (Coll and CollReg)
template <ncclFunc_t Fn, typename T, typename RedOp, int Algo, int Proto, int USE_ACC, int COLL_UNROLL, int Pipeline,
          int UserRegMode>
struct RunWorkBatch {
  // This __forceinline__ is necessary. The compiler was inserting a function call
  // here from the LL ncclKernel.
  __device__ __forceinline__ void run() {
    int tid = threadIdx.x;
    int tn = blockDim.x;

    if (RedOpArg<RedOp>::ArgUsed) {
      int nWorks = ncclShmem.nWorks;
      for (int w = tid; w < nWorks; w += tn) {
        struct ncclDevWorkColl* work = (ncclDevWorkColl*)(ncclShmem.workStorage + w * ncclShmem.workSize);
        if (work->redOpArgIsPtr) {
          work->redOpArg = RedOpArg<RedOp>::loadArg(reinterpret_cast<void*>(work->redOpArg));
        }
      }
      __syncthreads();
    }

#pragma unroll 1
    for (int w = 0; w < ncclShmem.nWorks; w++) {
      struct ncclDevWorkColl* work = (struct ncclDevWorkColl*)(ncclShmem.workStorage + w * ncclShmem.workSize);
      if (w != 0) {
        struct ncclDevWorkColl* workPrev =
          (struct ncclDevWorkColl*)(ncclShmem.workStorage + (w - 1) * ncclShmem.workSize);
        if (work->nWarps != workPrev->nWarps) __syncthreads();
      }
      int subtn = work->nWarps * WARP_SIZE;
#ifdef ENABLE_WARP_SPEED
      if (tid < subtn) {
        if (ncclShmem.warpComm == 0 || Algo != NCCL_ALGO_RING)
          RunWorkColl<Fn, T, RedOp, Algo, Proto>().run(tid, subtn, work);
        else if (ncclShmem.warpChannelId[tid / WARP_SIZE] >= 0)
          RunWorkColl<Fn, T, RedOp, Algo, Proto>().run(tid % WARP_SIZE, WARP_SIZE, work);
      }
#else
      // Coverity reports a possible thread divergence due to not all threads participating in the collective.
      // However, the code ensures that the participation is on a per-warp basis.
      // coverity[device_thread_diverged:FALSE]
      if (tid < subtn) RunWorkColl<Fn, T, RedOp, Algo, Proto>().run(tid, subtn, work);
#endif
    }
  }
};

#define START 0
#define STOP 1
#define FINI 2

__device__ __forceinline__ bool profilerEnabled(int workItemIdx) {
  return (ncclShmem.workType == ncclDevWorkTypeP2p) ?
           ((struct ncclDevWorkP2p*)ncclShmem.workStorage)[workItemIdx].profilerEnabled :
           ((struct ncclDevWorkColl*)ncclShmem.workStorage)[workItemIdx].profilerEnabled;
}

__device__ __forceinline__ void profiler(int action) {
  if (threadIdx.x == 0) {
    int idx = 0;
    uint64_t wc = ncclShmem.channel.workCounter + 1;
    if (action == START) {
      for (; wc <= ncclShmem.channel.workCounter + ncclShmem.nWorks; wc++) {
        if (!profilerEnabled(idx++)) continue;
        ncclShmem.comm.workStarted[ncclShmem.channelId].data[wc % MAX_PROFILER_EVENTS_PER_CHANNEL].timestamp =
          globaltimer();
        ncclShmem.comm.workStarted[ncclShmem.channelId].data[wc % MAX_PROFILER_EVENTS_PER_CHANNEL].counter = wc;
      }
    } else {
      for (; wc <= ncclShmem.channel.workCounter + ncclShmem.nWorks; wc++) {
        if (!profilerEnabled(idx++)) continue;
        ncclShmem.comm.workCompleted[ncclShmem.channelId].data[wc % MAX_PROFILER_EVENTS_PER_CHANNEL].timestamp =
          globaltimer();
        ncclShmem.comm.workCompleted[ncclShmem.channelId].data[wc % MAX_PROFILER_EVENTS_PER_CHANNEL].counter = wc;
      }
      ncclShmem.channel.workCounter += ncclShmem.nWorks;
      if (action == FINI)
        ((ncclKernelCommAndChannels*)ncclShmem.args.comm)->channels[ncclShmem.channelId].workCounter =
          ncclShmem.channel.workCounter;
    }
  }
}

template <int SpecializedFnId, typename SpecializedRunWorkBatch, int COLL_UNROLL>
__device__ __forceinline__ void ncclKernelMain(struct ncclDevKernelArgs const* args) {
  const int tid = threadIdx.x;
  int tn = blockDim.x;
  int x = tid;
  int total = 0, y;
  int num = MAXCHANNELS / CHANNELS_PER_MASK_WORD > 0 ? MAXCHANNELS / CHANNELS_PER_MASK_WORD : 1;
#ifdef ENABLE_WARP_SPEED
  int warpCount = tn / WARP_SIZE;
  int localWarpId = tid / WARP_SIZE;
  int globalWarpId = (warpCount * blockIdx.x) + localWarpId;
  int laneId = tid % WARP_SIZE;
#endif
  // Copy kernel args to shmem and then only read those. Otherwise the compiler
  // will end up putting the args into thread local stack which is very wasteful.
  if (tid < sizeof(ncclDevKernelArgs) / sizeof(uint32_t)) {
    ((uint32_t*)&ncclShmem.args)[tid] = ((uint32_t*)args)[tid];
  }

  // To map blockId to channelId, we need the n'th set bit of channelMask which
  // is the inverse of counting the number of set bits among the the first n.
  // PTX has the fns instruction which does this but is extremely slow. We can
  // do better when we know all threads are querying the same bitmask.
  switch (tid / WARP_SIZE) {
  case 0:
  // ncclShmem.channelId = blockIdx.x;
    for (int i = 0; i < num; i++) {
      // WARP_SIZE<64 path leaves `x` set to (WARP_SIZE+tid) from the
      // previous iteration, so the first check of masks[i] for i>=1 was reading
      // the upper 32 bits twice and never the lower 32 bits. Reset to tid here.
      x = tid;
      if (args->channelMask.masks[i] & (1ull << x)) {
        y = __popcll(args->channelMask.masks[i] & ((1ull << x) - 1));
        y = total + y;
        if (blockIdx.x == y) {
          // channelId is the absolute bit position in the global mask:
          // i*CHANNELS_PER_MASK_WORD + x. Using `x + total` was only correct
          // when prior mask words were densely packed (which broke for sparse
          // channel sets, e.g. SATURATE_P2P_NCHANNELS with small messages or
          // non-pow2 tilings, causing the wrong channel to be loaded -> IMA).
          ncclShmem.channelId = x + i * CHANNELS_PER_MASK_WORD;
          break;
        }
      }
      if (WARP_SIZE < 64) {
        x = WARP_SIZE + tid;
        if (args->channelMask.masks[i] & (1ull << x)) {
          y = __popcll(args->channelMask.masks[i] & ((1ull << x) - 1));
          y = y + total;
          if (blockIdx.x == y) {
            ncclShmem.channelId = x + i * CHANNELS_PER_MASK_WORD;
            break;
          }
        }
      }
      total = total + __popcll(args->channelMask.masks[i]);
    }
    break;
  case 1:
    if (tid < WARP_SIZE + NCCL_MAX_GROUPS) {
      if (tid == WARP_SIZE) ncclShmem.barrier_pat = 0;
      ncclShmem.groups[tid - WARP_SIZE].barrier = 0;
    }
    break;
  case 2:
#ifdef ENABLE_FAULT_INJECTION
    /* load faults injection before first sync threads */
    if (tid == 2 * WARP_SIZE) ncclShmem.faults = args->comm->faults;
#endif
    break;
  case 3:
    /* set abort flag to 0 */
    if (tid == 3 * WARP_SIZE) ncclShmem.aborted = 0;
    break;
  default:
    break;
  }
  __syncthreads(); // publish ncclShmem.{args, channelId}
  /* set abort flag to 0 */
  if (tid == 0) {
    ncclShmem.aborted = 0;
    ncclShmem.channel.workCounter =
      ((ncclKernelCommAndChannels*)ncclShmem.args.comm)->channels[ncclShmem.channelId].workCounter;
  }

  // Use first 2 warps to load comm and channel, and remaining load work batch.
  switch (tid / WARP_SIZE) {
  case 0:
    {
      void* dst = &ncclShmem.comm;
      void* src = ncclShmem.args.comm;
      int bytes = sizeof(ncclKernelComm);
      static_assert(sizeof(ncclKernelComm) <= 16 * WARP_SIZE,
                    "ncclKernelComm cannot be loaded by a single warp in one insn.");
      copyToShmem16(tid, dst, src, bytes);
    }
    break;
  case 1:
    { // Get address of channel without incurring indirect load from ncclKernelComm::channels
      void* dst = &ncclShmem.channel;
      void* src = &((ncclKernelCommAndChannels*)ncclShmem.args.comm)->channels[ncclShmem.channelId];
      int bytes = sizeof(ncclDevChannel);
      static_assert(sizeof(ncclDevChannel) <= 16 * WARP_SIZE,
                    "ncclDevChannel cannot be loaded by a single warp in one insn.");
      copyToShmem16(tid - WARP_SIZE, dst, src, bytes);
    }
    break;
  default:
    {
      int subtid = tid - 2 * WARP_SIZE;
      int subtn = tn - 2 * WARP_SIZE;
      // Coverity reports a possible thread divergence due to not all threads participating in the collective.
      // However, the code ensures that the participation is on a per-warp basis.
      // coverity[device_thread_diverged:FALSE]
      loadWorkBatchToShmem(subtid, subtn, args, /*batchIx=*/blockIdx.x);
    }
    break;
  }
#ifdef ENABLE_WARP_SPEED
  if (tid == 0) {
    ncclShmem.warpComm = args->warpLevelComm;
  }
#endif
  __syncthreads(); // publish shmem

#ifdef ENABLE_WARP_SPEED
  // Determine per-warp channel assignment for WarpSpeed enablement
  total = 0;
  if (ncclShmem.warpComm ==
      1) {  // If warpComm is enabled, assign warps to channels that have the corresponding channel mask enabled
    ncclShmem.warpChannelId[localWarpId] = -1;
    __syncthreads();
    for (int i = 0; i < num; i++) {
      if (args->channelMask.masks[i] & (1ull << laneId)) {
        y = __popcll(args->channelMask.masks[i] & ((1ull << laneId) - 1));
        y = total + y;
        if (globalWarpId == y) {
          // Same fix as the non-WS path: channelId is the absolute bit
          // position (i*CHANNELS_PER_MASK_WORD + laneId), not total bits
          // seen so far.
          ncclShmem.warpChannelId[localWarpId] = laneId + i * CHANNELS_PER_MASK_WORD;
          break;
        }
      }
      total = total + __popcll(args->channelMask.masks[i]);
    }
    __syncthreads();
    if (ncclShmem.warpChannelId[localWarpId] >= 0) {
      void* dst = &ncclShmem.warpChannel[localWarpId];
      void* src = &((ncclKernelCommAndChannels*)ncclShmem.args.comm)->channels[ncclShmem.warpChannelId[localWarpId]];
      int bytes = sizeof(ncclDevChannel);
      static_assert(sizeof(ncclDevChannel) <= 16 * WARP_SIZE,
                    "ncclDevChannel cannot be loaded by a single warp in one insn.");
      // assert((tid-localWarpId*WARP_SIZE) >= 0 && (tid-localWarpId*WARP_SIZE) < WARP_SIZE);
      copyToShmem16(tid - localWarpId * WARP_SIZE, dst, src, bytes);
    }
  } else {  // warpComm disabled: skip per-warp channel copy; readers fall back to ncclShmem.channel
    if (laneId == 0) {
      ncclShmem.warpChannelId[localWarpId] = ncclShmem.channelId;
    }
  }
  __syncthreads();
#endif

  while (ncclShmem.aborted == 0) {
    profiler(START);
    if (0 <= SpecializedFnId && ncclShmem.funcId == (unsigned)SpecializedFnId) {
      SpecializedRunWorkBatch().run();
    } else {
#if defined(USE_INDIRECT_FUNCTION_CALL) || defined(RCCL_DEVICE_LINKER)
      if (COLL_UNROLL == 1) ncclDevFuncTable_1[ncclShmem.funcId]();
      else if (COLL_UNROLL == 2) ncclDevFuncTable_2[ncclShmem.funcId]();
      else if (COLL_UNROLL == 4) ncclDevFuncTable_4[ncclShmem.funcId]();
      else if (COLL_UNROLL == 8) ncclDevFuncTable_8[ncclShmem.funcId]();
      else if (COLL_UNROLL == 16) ncclDevFuncTable_16[ncclShmem.funcId]();
      else ncclDevFuncTable_32[ncclShmem.funcId]();
#else
      if (COLL_UNROLL == 1) NCCL_CALL_FUNCTIONS_1(ncclShmem.funcId);
      else if (COLL_UNROLL == 2) NCCL_CALL_FUNCTIONS_2(ncclShmem.funcId);
      else if (COLL_UNROLL == 4) NCCL_CALL_FUNCTIONS_4(ncclShmem.funcId);
      else if (COLL_UNROLL == 8) NCCL_CALL_FUNCTIONS_8(ncclShmem.funcId);
      else if (COLL_UNROLL == 16) NCCL_CALL_FUNCTIONS_16(ncclShmem.funcId);
      else NCCL_CALL_FUNCTIONS_32(ncclShmem.funcId);
#endif
    }

    if (ncclShmem.nextBatchIx == -1) break;
    int batchIx = ncclShmem.nextBatchIx;
    __syncthreads();
    switch (tid / WARP_SIZE) {
    case 1:
      if (tid < WARP_SIZE + NCCL_MAX_GROUPS) {
        if (tid == WARP_SIZE) ncclShmem.barrier_pat = 0;
        ncclShmem.groups[tid - WARP_SIZE].barrier = 0;
      }
      break;
    default:
      break;
    }
    profiler(STOP);
    loadWorkBatchToShmem(tid % WARP_SIZE, tn, args, batchIx);
    __syncthreads();
  }
  profiler(FINI);
}

__global__ void ncclDevKernel_Generic_1(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);
__global__ void ncclDevKernel_Generic_2(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);
__global__ void ncclDevKernel_Generic_4(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);
__global__ void ncclDevKernel_Generic_8(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);
__global__ void ncclDevKernel_Generic_16(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);
__global__ void ncclDevKernel_Generic_32(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage);

#define DEFINE_ncclDevKernel_nop(suffix, coll, redop, ty, algo, proto, specializedFnId) \
  __global__ void ncclDevKernel_##suffix(ncclDevKernelArgsDefaultStorage NCCL_GRID_CONSTANT const argsStorage) {}

// noinline iff RCCL_DEVICE_LINKER (each devfunc is a standalone shard).
#ifdef RCCL_DEVICE_LINKER
#define DEFINE_ncclDevFunc(suffix, coll, redop, ty, algo, proto, acc, pipeline, unroll, userreg) \
  __device__ __attribute__((noinline)) void ncclDevFunc_##suffix() { \
    RunWorkBatch<coll, ty, redop<ty>, algo, proto, acc, unroll, pipeline, userreg>().run(); \
  }
#else
#define DEFINE_ncclDevFunc(suffix, coll, redop, ty, algo, proto, acc, pipeline, unroll, userreg) \
  __device__ void ncclDevFunc_##suffix() { \
    RunWorkBatch<coll, ty, redop<ty>, algo, proto, acc, unroll, pipeline, userreg>().run(); \
  }
#endif

#endif
